  //JCL_lstMake_Join
  //CL01_lstCL_Make_Join
  //20130309
  //別テーブルから名前をジョイン

C_POINTER($1;$tblPtr)
$tblPtr:=$1  //テーブルポインタ
C_POINTER($2;$IDFldPtr)
$IDFldPtr:=$2  //ＩＤフィールドのポインタ
C_POINTER($3;$NameFldPtr)
$NameFldPtr:=$3  //名前フィールドのポインタ
C_POINTER($4;$IDAryPtr)
$IDAryPtr:=$4  //ＩＤの配列
C_POINTER($5;$NameAryPtr)
$NameAryPtr:=$5  //返す、名前の配列
ARRAY LONGINT($aryID;0)
ARRAY TEXT($aryName;0)
C_LONGINT($i;$sizeOfAry)

  //キャッシュ対象のテーブルからIDと名前を配列に取得
READ ONLY($tblPtr->)
ALL RECORDS($tblPtr->)
FIRST RECORD($tblPtr->)
SELECTION TO ARRAY($IDFldPtr->;$aryID)
SELECTION TO ARRAY($NameFldPtr->;$aryName)

$sizeOfAry:=Size of array($IDAryPtr->)
For ($i;1;$sizeOfAry)
	
	$pos:=Find in array($aryID;$IDAryPtr->{$i})
	If ($pos>0)
		
		$NameAryPtr->{$i}:=$aryName{$pos}
		
	Else 
		$NameAryPtr->{$i}:=""
		
	End if 
	
End for 
