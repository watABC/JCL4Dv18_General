  //JCL_file_SelectFileDlg
  //20121129 wat
  //ファイルオープンダイアログを表示してファイルの中身を返す

C_POINTER($1)  //ブロブのポインタ
C_LONGINT($0;$open_ok)

C_TIME($fileRef)  //ドキュメント参照
C_LONGINT($fileSize)  //読み込むファイルのサイズ
C_BLOB($fileBlob)  //読み込んだファイルの内容
C_LONGINT($maxFIleSize)
$maxFIleSize:=1024*1024*1024  //開くファイルサイズの上限

  //ブロブのサイズをゼロにする。何もしないとゴミがが入る
SET BLOB SIZE($fileBlob;0)

  //エラーコール
ON ERR CALL("JCL_file_OnErrorCall")

  //ファイルを読み込み専用で開く
$fileRef:=Open document("";Read mode)
$open_ok:=OK
If ($open_ok=1)
	
	  //上限サイズより大きいか？
	$fileSize:=Get document size($fileRef)
	If ($fileSize<=$maxFileSize)
		
		  //ファイルの最後まで内容をブロブに取得
		RECEIVE PACKET($fileRef;$fileBlob;$fileSize)
		
	End if 
	
	  //ファイルを閉じる
	CLOSE DOCUMENT($fileRef)
	
End if 

  //エラーコール終了
ON ERR CALL("")

$1->:=$fileBlob
$0:=$open_ok
