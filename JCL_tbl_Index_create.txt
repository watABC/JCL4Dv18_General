  //JCL_tbl_Index_create
  //JCL_tbl_CreatIndex
  //20121001 wat rename, 20130430 mod
  //テーブルを作成

C_TEXT($1;$inFileText)
$inFileText:=$1  //読み込んだファイルの中身
C_TEXT($2;$inTableName)
$inTableName:=$2  //テーブル名
C_LONGINT($3;$inStartLineNr)
$inStartLineNr:=$3  //テーブル定義始まりの行番号,スタート行番号
C_TEXT($4;$inPrefix)
$inPrefix:=$4  //フィールドのプリフィックス
C_LONGINT($0;$cnt)
ARRAY TEXT($lineAry;0)
ARRAY TEXT($itemAry;0)
C_LONGINT($numOfLines;$numOfItems)
ARRAY TEXT($aryFieldName;0)  //フィールド名の配列
ARRAY TEXT($aryFieldType;0)  //フィールドタイプの配列
ARRAY TEXT($aryCharLength;0)  //文字長さの配列
ARRAY TEXT($aryIndex;0)  //インデックス
C_POINTER($fldPtr)
C_TEXT($fldName)
C_LONGINT($retCode)
ARRAY POINTER($fldAry;0)

  //改行で切り分ける
$numOfLines:=JCL_str_Extract ($inFileText;Char(Carriage return);->$lineAry)

  //スタート行番号の次の行からフィールド定義を得る
For ($i;$inStartLineNr+1;$numOfLines)
	
	DELETE FROM ARRAY($itemAry;1;Size of array($itemAry))
	$numOfItems:=JCL_str_Extract ($lineAry{$i};Char(Tab);->$itemAry)
	If (4<=$numOfItems)
		If ($itemAry{1}#"_")
			  //最初のセルが＿（アンダーバー）じゃなかったらフィールド情報取得
			APPEND TO ARRAY($aryFieldName;$itemAry{1})  //フィールド名
			APPEND TO ARRAY($aryFieldType;$itemAry{2})  //タイプ
			APPEND TO ARRAY($aryCharLength;$itemAry{3})  //文字長さ
			APPEND TO ARRAY($aryIndex;$itemAry{4})  //インデックス
			
			  //インデックス作成
			If ($itemAry{4}="1")
				
				$tblPtr:=JCL_tbl_Ptr_byName ($inTableName)
				$fldName:=$inPrefix+"_"+$itemAry{1}  //20130501
				$retCode:=JCL_tbl_Fld_GetPtr ($tblPtr;$fldName;->$fldPtr)
				If ($retCode=0)
					
					DELETE FROM ARRAY($fldAry;1;Size of array($fldAry))
					APPEND TO ARRAY($fldAry;$fldPtr)
					
					$indexName:=$inPrefix+$itemAry{1}+"index"
					CREATE INDEX($tblPtr->;$fldAry;0;$indexName)
					
				End if 
			End if 
		Else 
			  //最初のセルが＿（アンダーバー）だったらフィールド情報取得は終わり
			$i:=$numOfLines+1  //ループを抜ける
			
		End if 
	Else 
		$i:=$numOfLines+1  //ループを抜ける
		
	End if 
End for 

$0:=$cnt
