  //JCL_Add_byInitValues_generate
  //20140810 yabe

C_LONGINT($i;$k)
C_TEXT($tableName)
C_TEXT($fieldName)

C_LONGINT($type)
C_LONGINT($len)
C_BOOLEAN($index)
C_BOOLEAN($unique)
C_BOOLEAN($visible)

ARRAY TEXT($aryExtract;0)
C_TEXT($prefix)
C_TEXT($methodName)
C_TEXT($method)
C_TEXT($fieldMethod)
C_TEXT($initValue)

C_BLOB($blob)
ARRAY LONGINT($propertiesArray;7)
$propertiesArray{1}:=1  //メソッドが表示の時1、非表示なら0。
$propertiesArray{2}:=0  //メソッドが4DACTION、4DMETHOD、4DSCRIPTから利用可能であれば1、そうでなければ0。
$propertiesArray{3}:=0  //メソッドをWebサービスとして提供する場合1、そうでなければ0。
$propertiesArray{4}:=0  //propertiesArray{3}=1のとき、メソッドをWSDLで公開する場合1、そうでなければ0。
$propertiesArray{5}:=0  //メソッドをコンポーネントとホストデータベースで共有する場合1、そうでなければ0。
$propertiesArray{6}:=0  //メソッドがSQLから利用可能であれば1、そうでなければ0。
$propertiesArray{7}:=0  //メソッドに"サーバ上で実行"属性を設定する場合は1、そうでなければ0。

For ($i;1;Get last table number)
	  //For ($i;1;1)
	
	If (Is table number valid($i)=True)
		
		$tableName:=Table name($i)
		$prefix:=""
		$method:=""
		$fieldMethod:=""
		
		For ($k;1;Get last field number($i))
			
			If (Is field number valid($i;$k)=True)
				
				$fieldName:=Field name($i;$k)
				
				GET FIELD PROPERTIES($i;$k;$type;$len;$index;$unique;$visible)
				
				DELETE FROM ARRAY($aryExtract;1;Size of array($aryExtract))
				
				If (($type#Is picture) & ($type#Is subtable) & ($type#Is BLOB))
					Case of 
						: ($type=Is date)
							$initValue:="!0000/00/00!"
						: ($type=Is time)
							$initValue:="Time(\"00:00:00\")"
						Else 
							If ((Position("CREATE_DATEMARK";$fieldName)#0) | (Position("UPDATE_DATEMARK";$fieldName)#0))
								$initValue:="JCL_str_Datemark (Current date;Current time)"
							Else 
								$initValue:=JCL_tbl_InitValue (JCL_tbl_Type ($type))
							End if 
					End case 
					$fieldMethod:=$fieldMethod+"["+$tableName+"]"+$fieldName+":="+$initValue+Char(Carriage return)
				End if 
				
				JCL_str_Extract ($fieldName;"_";->$aryExtract)
				
				If (Size of array($aryExtract)>=1)
					$prefix:=$aryExtract{1}
				End if 
				
			End if 
			
		End for 
		
		If ($prefix#"")
			
			$methodName:=$prefix+"_Add_byInitValues"
			
			$method:=$method+"//"+$prefix+"_Add_byInitValues"+Char(Carriage return)
			$method:=$method+"//method_generator"+Char(Carriage return)
			$method:=$method+"//"+$tableName+" レコード追加、初期値代入"+Char(Carriage return)
			$method:=$method+Char(Carriage return)
			$method:=$method+"C_LONGINT($0;$"+$prefix+"_id)"
			$method:=$method+Char(Carriage return)
			$method:=$method+"//新規レコード作成"+Char(Carriage return)
			$method:=$method+"$"+$prefix+"_id:=JCL_Sequence number (\""+$prefix+"_ID\";\""+$tableName+"\")"+Char(Carriage return)
			$method:=$method+"CREATE RECORD(["+$tableName+"])"+Char(Carriage return)
			$method:=$method+Char(Carriage return)
			$method:=$method+$fieldMethod
			$method:=$method+"["+$tableName+"]"+$prefix+"_ID:=$"+$prefix+"_id"+Char(Carriage return)
			$method:=$method+Char(Carriage return)
			$method:=$method+"SAVE RECORD(["+$tableName+"])"+Char(Carriage return)
			$method:=$method+Char(Carriage return)
			$method:=$method+"$0:=$"+$prefix+"_id"+Char(Carriage return)
			
			SET BLOB SIZE($blob;0)
			TEXT TO BLOB($method;$blob;Mac C string)
			
			  //メソッドがなければ作成
			  //v14
			METHOD SET CODE($methodName;$method)
			  //If (AP Does method exist($methodName)=0)
			  //AP Create method($methodName;$propertiesArray;$blob)
			  //Else 
			  //AP Modify method($methodName;$blob)
			  //End if 
			
		End if 
		
	End if 
	
End for 

ALERT("完了")
