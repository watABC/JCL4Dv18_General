  //JCL_method_import
  //20150524 yabe wat v14対応
  //20150911 wat コンポーネントで共有
  //20181226 wat 不可視ファイルを無視する

C_TEXT($dir)  //メソッドを読み込むディレクトリ
C_LONGINT($i;$numOfFile)
C_TEXT($filePath)
ARRAY TEXT($aryFileList;0)

ARRAY LONGINT($propertiesArray;7)
$propertiesArray{1}:=1  //メソッドが表示の時1、非表示なら0。
$propertiesArray{2}:=0  //メソッドが4DACTION、4DMETHOD、4DSCRIPTから利用可能であれば1、そうでなければ0。
$propertiesArray{3}:=0  //メソッドをWebサービスとして提供する場合1、そうでなければ0。
$propertiesArray{4}:=0  //propertiesArray{3}=1のとき、メソッドをWSDLで公開する場合1、そうでなければ0。
$propertiesArray{5}:=0  //メソッドをコンポーネントとホストデータベースで共有する場合1、そうでなければ0。
$propertiesArray{6}:=0  //メソッドがSQLから利用可能であれば1、そうでなければ0。
$propertiesArray{7}:=0  //メソッドに"サーバ上で実行"属性を設定する場合は1、そうでなければ0。

C_TEXT($methodName;$method)
$method:=""
C_BLOB($blob)
C_TEXT($retText)
$retText:="end"

$dir:=Select folder("")

If (OK=1)
	  //20181226 wat 不可視ファイルを無視する
	DOCUMENT LIST($dir;$aryFileList;Ignore invisible)
	
	$numOfFile:=Size of array($aryFileList)
	
	  //4Dにメソッドが認識されるように、はじめに空のメソッドを作成しておく
	For ($i;1;$numOfFile)
		
		$methodName:=$aryFileList{$i}
		$method:=""
		METHOD SET CODE($methodName;$method)
		
	End for 
	
	For ($i;1;$numOfFile)
		
		$filePath:=$dir+$aryFileList{$i}
		$methodName:=$aryFileList{$i}
		
		  //v14
		DOCUMENT TO BLOB($filePath;$blob)
		$method:=BLOB to text($blob;UTF8 text without length)
		
		
		METHOD SET CODE($methodName;$method)
		
		METHOD SET ATTRIBUTE($methodName;Attribute shared;True)  // 20150911 wat
		
	End for 
	
	ALERT($retText)
	
End if 
