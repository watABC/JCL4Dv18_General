  //jcl_method_export
  //20130429 yabe new
  //20190107 wat ike 改行はＬＦ、BLOB経由しない、ファイル名はJCL_strで生成

C_TEXT($dir)  //メソッドを書き出すディレクトリ
C_TEXT($folderName;$dateStr;$timeStr)  //メソッド入れるフォルダの名前
  //C_DATE($date)
  //$date:=Current date
  //$dateStr:=String(Year of($date);"####")
  //$dateStr:=$dateStr+String(Month of($date);"00")
  //$dateStr:=$dateStr+String(Day of($date);"00")
  //$timeStr:=Time string(Current time)
  //$timeStr:=Replace string($timeStr;":";"")
  //$folderName:="method"+$dateStr+$timeStr
$folderName:="method"+JCL_str_Datemark (Current date;Current time)

C_LONGINT($i;$numOfMethod)
C_TEXT($method)
C_TEXT($methodFilePath)
ARRAY TEXT($aryMethodName;0)
C_BLOB($blob)

  //すべてのプロジェクトメソッド名を配列に取得
METHOD GET NAMES($aryMethodName)

$dir:=Select folder("")
If (OK=1)
	  //セレクトフォルダは最後にパスセパレータを含む文字列を返す
	  //それを利用して、最後の文字をセパレータとして取得（OSを判定するコードは不要になる）
	$separator:=Substring($dir;Length($dir);1)
	
	  //選択されたディレクトリにフォルダ作成
	CREATE FOLDER($dir+$folderName)
	
	$numOfMethod:=Size of array($aryMethodName)
	For ($i;1;$numOfMethod)
		  //メソッドコードを取得
		METHOD GET CODE($aryMethodName{$i};$method)
		
		  //メソッドを保存するファイルパス作成
		$methodFilePath:=$dir+$folderName
		$methodFilePath:=$methodFilePath+$separator
		$methodFilePath:=$methodFilePath+$aryMethodName{$i}
		
		  //SET BLOB SIZE($blob;0)
		  //TEXT TO BLOB($method;$blob;UTF8 text without length)
		  //BLOB TO DOCUMENT($methodFilePath;$blob)
		
		  //20190107 wat ike 改行はＬＦ、BLOB経由しない
		TEXT TO DOCUMENT($methodFilePath;$method;"UTF-8";Document with LF)
		
	End for 
	ALERT("完了")
	
End if 
