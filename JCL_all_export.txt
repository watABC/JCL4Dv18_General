  //JCL_all_export
  //20140810 yabe
  //20150902 yabe jiro_dlg対応

C_LONGINT($open_dlg_ok)
C_TEXT($folder_Path)
ARRAY TEXT($aryFileList;0)

  //フォルダダイアログ表示
$open_dlg_ok:=JCL_file_SelectFolder (->$folder_Path;->$aryFileList)
If ($open_dlg_ok=1)  //フォルダが選択された？
	
	C_BOOLEAN($done)
	JCL_pgs_DefInit 
	JCL_pgs_Show ("全データ書き出し中")
	
	C_LONGINT($i;$k)
	
	C_TEXT($name)
	C_LONGINT($type)
	C_LONGINT($len)
	C_BOOLEAN($index)
	C_BOOLEAN($unique)
	C_BOOLEAN($visible)
	
	C_LONGINT($open_ok)
	C_TEXT($fileName)
	C_TIME($FileRef)
	C_TEXT($field)
	
	$i:=1
	While (($i<=Get last table number) & ($done=False))
		
		If (Is table number valid($i)=True)
			
			$fileName:=$folder_Path+Table name($i)+".txt"
			$open_ok:=JCL_file_OpenForWrite ($fileName;->$FileRef)
			
			If ($open_ok=1)
				
				  // 20140923 「ID」という文字がファイルの先頭にあるとエクセルがSYLKファイルと勘違いするため先頭に空白を出力
				JCL_file_WriteSJIS ($FileRef;" ")
				
				$tablePrt:=Table($i)
				READ ONLY($tablePrt->)
				ALL RECORDS($tablePrt->)
				FIRST RECORD($tablePrt->)
				$numOfAllRecs:=Records in selection($tablePrt->)
				
				  //フィールド名を書き出し
				For ($k;1;Get last field number($i))
					
					If (Is field number valid($i;$k)=True)
						
						$fieldPtr:=Field($i;$k)
						$name:=Field name($i;$k)
						
						JCL_file_WriteSJIS ($FileRef;$name)
						JCL_file_WriteTab ($FileRef)
						
					End if 
					
				End for 
				
				JCL_file_WriteCRLF ($FileRef)
				
				  //レコードを書き出し
				$recCount:=1
				While ((Not(End selection($tablePrt->))) & ($done=False))
					  //While (Not(End selection($tablePrt->)))
					
					For ($k;1;Get last field number($i))
						
						If (Is field number valid($i;$k)=True)
							
							$fieldPtr:=Field($i;$k)
							GET FIELD PROPERTIES($i;$k;$type;$len;$index;$unique;$visible)
							Case of 
								: ($type=Is alpha field)
									$field:=$fieldPtr->
									$field:=Replace string($field;Char(Carriage return);"<br>")
									$field:=Replace string($field;Char(Line feed);"<br>")
								: ($type=Is text)
									$field:=$fieldPtr->
									$field:=Replace string($field;Char(Carriage return);"<br>")
									$field:=Replace string($field;Char(Line feed);"<br>")
								: ($type=Is real)
									$field:=String($fieldPtr->)
								: ($type=Is integer)
									$field:=String($fieldPtr->)
								: ($type=Is longint)
									$field:=String($fieldPtr->)
								: ($type=Is date)
									$field:=String($fieldPtr->)
								: ($type=Is time)
									$field:=String($fieldPtr->)
								: ($type=Is boolean)
									If ($fieldPtr->=True)
										$field:="True"
									Else 
										$field:="False"
									End if 
								: ($type=Is picture)
									  //書き出ししない
								: ($type=Is subtable)
									  //書き出ししない
								: ($type=Is BLOB)
									  //書き出ししない
							End case 
							
							JCL_file_WriteSJIS ($FileRef;$field)
							JCL_file_WriteTab ($FileRef)
							
							JCL_pgs_SetValue (($i/Get last table number)*100;Table name($i);$recCount;$numOfAllRecs)
							$done:=JCL_pgs_IsCancel 
							
						End if 
						
					End for 
					
					JCL_file_WriteCRLF ($FileRef)
					NEXT RECORD($tablePrt->)
					$recCount:=$recCount+1
					
				End while 
				
			End if 
			
			  //ファイルを閉じる
			JCL_file_Close ($FileRef)
			
		End if 
		
		$i:=$i+1
		
	End while 
	
	JCL_pgs_Cancel 
	
	  //終了メッセージ
	If ($done=True)
		ALERT("出力がキャンセルされました。")
	Else 
		ALERT("出力が終わりました。")
	End if 
	
End if 
