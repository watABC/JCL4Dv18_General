  //JCL_lst_GetSelectedValues
  //JCL_lst_GetSelectedLongAry
  //20130128 wat
  //リストボックスの選択されている行の値配列を取得

C_POINTER($1;$inListBoxPtr)
$inListBoxPtr:=$1  //リストボックスのブーリアン配列
C_POINTER($2;$inAryPtr)
$inAryPtr:=$2  //リストに表示されているデータ配列のポインタ
C_POINTER($3;$outAryPtr)
$outAryPtr:=$3  //選択されているデータ配列のポインタ（返す）
C_LONGINT($0;$cnt)  //配列数
$cnt:=0

C_LONGINT($selCnt)
C_LONGINT($sizeOfAry;$i)

  //配列をクリア
DELETE FROM ARRAY($outAryPtr->;1;Size of array($outAryPtr->))

  //空行がクリックされたら、配列クリアのまま
$selCnt:=JCL_lst_SelectedCount ($inListBoxPtr)
If ($selCnt>=1)
	
	  //選択されている ID を取得
	$sizeOfAry:=Size of array($inAryPtr->)
	For ($i;1;$sizeOfAry)
		
		If ($inListBoxPtr->{$i}=True)
			
			APPEND TO ARRAY($outAryPtr->;$inAryPtr->{$i})
			$cnt:=$cnt+1
			
		End if 
		
	End for 
	
End if 

$0:=$cnt
