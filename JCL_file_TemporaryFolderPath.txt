  //JCL_file_TemporaryFolderPath
  //20160823 yabe
  //一時ファイルを保存するパスを返す
  // 20160907 wat 重複を避けるためデイトマークを入れる

C_TEXT($1;$fileName)
$fileName:=$1

C_TEXT($0;$temporaryFolderPath)

C_TEXT($ext)
$ext:="."+JCL_file_Extension ($fileName)

C_TEXT($dirName)
$dirName:="MIPS"

C_TEXT($tempPath)
$tempPath:=JCL_file_MakeFilePath (Temporary folder;$dirName)

C_LONGINT($no)
$no:=0

If (Test path name($tempPath)#Is a folder)
	CREATE FOLDER($tempPath)
End if 

  //20160926 yabe ファイル名の重複を避けてパスを取得
$temporaryFolderPath:=JCL_file_MakeFilePath ($tempPath;Jiro_str_CreateDatemark (Current date;Current time)+$ext)
While (Test path name($temporaryFolderPath)=Is a document)
	$no:=$no+1
	$temporaryFolderPath:=JCL_file_MakeFilePath ($tempPath;Jiro_str_CreateDatemark (Current date;Current time)+"("+String($no)+")"+$ext)
End while 

$0:=$temporaryFolderPath
