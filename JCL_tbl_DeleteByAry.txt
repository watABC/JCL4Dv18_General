  //JCL_tbl_DeleteByAry
  //20130311 wat
  //テーブルと検索対象フィールドを与えて、配列の要素で検索して物理削除

  //CL01_DeleteMany
  //20130311
  //複数を物理削除

C_POINTER($1;$tblPtr)
$tblPtr:=$1  //テーブルポインタ
C_POINTER($2;$IDFldPtr)
$IDFldPtr:=$2  //ＩＤフィールドのポインタ
C_POINTER($3;$IDAryPtr)
$IDAryPtr:=$3  //ＩＤの配列

C_LONGINT($i;$sizeOfAry)
C_LONGINT($0;$delCnt)
$delCnt:=0

READ WRITE($tblPtr->)

  //リストボックスのすべての行について
$sizeOfAry:=Size of array($IDAryPtr->)
For ($i;1;$sizeOfAry)
	
	  //IDで検索して物理削除
	QUERY($tblPtr->;$IDFldPtr->=$IDAryPtr->{$i})
	FIRST RECORD($tblPtr->)
	DELETE SELECTION($tblPtr->)
	
	$delCnt:=$delCnt+1
	
End for 
UNLOAD RECORD($tblPtr->)
READ ONLY($tblPtr->)

$0:=$delCnt
