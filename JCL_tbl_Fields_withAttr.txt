  //JCL_tbl_Fields_withAttr
  //JCL_tbl_GetFields
  //20130430 yabe
  //フィールド属性を配列で返す

C_TEXT($1;$tableName)
$tableName:=$1
C_POINTER($2;$ioAryFieldName)
$ioAryFieldName:=$2
C_POINTER($3;$ioAryFieldType)
$ioAryFieldType:=$3
C_POINTER($4;$ioAryFieldLength)
$ioAryFieldLength:=$4
C_POINTER($5;$ioAryFieldIndex)
$ioAryFieldIndex:=$5

C_LONGINT($i;$numOfTables)
C_LONGINT($k;$numOfFields)
C_TEXT($name)
C_LONGINT($type)
C_LONGINT($len)
C_BOOLEAN($index)
C_BOOLEAN($unique)
C_BOOLEAN($visible)

DELETE FROM ARRAY($ioAryFieldName->;1;Size of array($ioAryFieldName->))
DELETE FROM ARRAY($ioAryFieldType->;1;Size of array($ioAryFieldType->))
DELETE FROM ARRAY($ioAryFieldLength->;1;Size of array($ioAryFieldLength->))
DELETE FROM ARRAY($ioAryFieldIndex->;1;Size of array($ioAryFieldIndex->))

$numOfTables:=Get last table number

For ($i;1;$numOfTables)
	
	If (Table name($i)=$tableName)
		
		$numOfFields:=Get last field number($i)
		
		For ($k;1;$numOfFields)
			
			$name:=Field name($i;$k)
			GET FIELD PROPERTIES($i;$k;$type;$len;$index;$unique;$visible)
			
			APPEND TO ARRAY($ioAryFieldName->;$name)
			APPEND TO ARRAY($ioAryFieldType->;JCL_tbl_Type ($type))
			APPEND TO ARRAY($ioAryFieldLength->;String($len))
			APPEND TO ARRAY($ioAryFieldIndex->;String(Num($index);"1;;0"))
			
		End for 
		
	End if 
	
End for 